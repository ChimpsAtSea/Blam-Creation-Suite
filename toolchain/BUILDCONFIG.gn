
declare_args() {
  target_config = "debug"
  target_link_config = "dynamic"
  bcs_root = "//"
  bcs_third_party = "//thirdparty"
}

if (target_os == "") {
  target_os = host_os
}
if (target_cpu == "") {
  target_cpu = host_cpu
}
if (current_cpu == "") {
  current_cpu = target_cpu
}
if (current_os == "") {
  current_os = target_os
}

is_target_linux = current_os == "linux"
is_target_mac = current_os == "mac"
is_target_windows = current_os == "windows"
is_target_webassembly = current_os == "webassembly"
is_target_32bit = current_cpu == "x86" || current_cpu == "wasm32" || current_cpu == "arm"
is_target_64bit = !is_target_32bit

is_host_windows = host_os == "win"
is_host_linux = host_os == "linux"

bcs_library_type = "static_library"
bcs_is_shared = false
bcs_is_static = true
bcs_is_profile = true
if(is_target_windows)
{
  if (target_link_config == "static")
  {
    bcs_library_type = "static_library"
    bcs_is_shared = false
    bcs_is_static = true
    bcs_is_profile = false
  }
  else if (target_link_config == "dynamic")
  {
    bcs_library_type = "shared_library"
    bcs_is_shared = true
    bcs_is_static = false
    bcs_is_profile = false
  }
  else if (target_link_config == "staticprofile")
  {
    bcs_library_type = "static_library"
    bcs_is_shared = false
    bcs_is_static = true
    bcs_is_profile = true
  }
  else if (target_link_config == "dynamicprofile")
  {
    bcs_library_type = "shared_library"
    bcs_is_shared = true
    bcs_is_static = false
    bcs_is_profile = true
  }
  else
  {
    print("Unknown target_link_config " + target_link_config)
  }
}

# All binary targets will get this list of configs by default.
_shared_binary_target_configs = [ "//toolchain:compiler_defaults" ]

# Apply that default list to the binary target types.
set_defaults("executable") {
  configs = _shared_binary_target_configs
  configs += [ "//toolchain:binary_library_ldconfig" ]

  # Executables get this additional configuration.
  configs += [ "//toolchain:executable_ldconfig" ]
}
set_defaults("static_library") {
  configs = _shared_binary_target_configs

  # Static libraries get this additional configuration.
  configs += [ "//toolchain:static_library_ldconfig" ]
}
set_defaults("shared_library") {
  configs = _shared_binary_target_configs
  configs += [ "//toolchain:binary_library_ldconfig" ]

  # Shared libraries get this additional configuration.
  configs += [ "//toolchain:shared_library_ldconfig" ]
}
set_defaults("source_set") {
  configs = _shared_binary_target_configs
}

set_default_toolchain("//toolchain:clang")
