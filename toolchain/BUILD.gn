
llvm_bin = rebase_path(bcs_third_party + "/llvm/bin")

clang = string_join("/", [llvm_bin, "clang"])
ar = string_join("/", [llvm_bin, "llvm-ar"])
ld = string_join("/", [llvm_bin, "lld"])
not_needed([ 
  "ld", 
  "ar" ])

object_extension = ""
static_library_extension = ""
shared_library_extension = ""
executable_extension = ""
  
if(is_windows)
{
  object_extension = ".obj"
  static_library_extension = ".lib"
  shared_library_extension = ".dll"
  executable_extension = ".exe"
  
  lib_switch = "/defaultlib:"
  lib_dir_switch = "/libpath:"
  ld = string_join("/", [llvm_bin, "lld-link"])
} 
else if(is_webassembly)
{
  object_extension = ".o"
  static_library_extension = ".a"
  shared_library_extension = ""
  executable_extension = ".wasm"
  
  ld = string_join("/", [llvm_bin, "wasm-ld"])
} 
else if(is_linux)
{
  object_extension = ".o"
  static_library_extension = ".a"
  shared_library_extension = ".so"
  executable_extension = ""
  
  ld = string_join("/", [llvm_bin, "ld.lld"])
}

windows_default_libs = [ "libcmt.lib" ]
not_needed([ 
  "windows_default_libs", 
  "shared_library_extension" ])

config("compiler_defaults") {
  cflags = [ ]
  ldflags = [ ]
  if (is_linux) {
    cflags += [
      "-fPIC",
      "-pthread",
    ]
  }
  if (is_windows) {
    ldflags += [ ]
    cflags += [ "-g" ]
    if(current_config == "debug")
    {
      cflags += [ ]
    }
    if(current_config == "test")
    {
      cflags += [ ]
    }
    if(current_config == "release")
    {
      cflags += [ ]
    }
  }
  if (is_webassembly) {
    ldflags += [ ]
    cflags += [ "--no-standard-libraries" ]
    ldflags += [ "--no-entry" ]
    if(current_config == "debug")
    {
      cflags += [ ]
    }
    if(current_config == "test")
    {
      cflags += [ ]
    }
    if(current_config == "release")
    {
      cflags += [ ]
    }
  }
  
  clang_os_target = "unknown"
  # $TODO: for mingw builds, we need to differentiate between building ON and FOR windows
  if(is_windows)
  {
    clang_os_target = "windows-msvc"
  }
  if(is_linux)
  {
    clang_os_target = "pc-linux-gnu"
  }
  if(is_webassembly)
  {
    # $TODO: assumption
    clang_os_target = ""
  }
  
  target_tuple_flags = [ ]
  if(current_cpu == "arm")
  {
    target_tuple_flags += [ "-target", "arm-$clang_os_target" ]
  }
  if(current_cpu == "arm64")
  {
    target_tuple_flags += [ "-target", "aarch64-$clang_os_target" ]
  }
  if(current_cpu == "x86")
  {
    target_tuple_flags += [ "-target", "i386-$clang_os_target" ]
  }
  if(current_cpu == "x64")
  {
    target_tuple_flags += [ "-target", "x86_64-$clang_os_target" ]
  }
  if(current_cpu == "wasm32")
  {
    target_tuple_flags += [ "-target", "wasm32" ]
  }
  if(current_cpu == "wasm64")
  {
    target_tuple_flags += [ "-target", "wasm64" ]
    ldflags += [ "-mwasm64" ]
  }
  cflags += target_tuple_flags
}

config("binary_library_ldconfig") {
  ldflags = [ ]
  libs = [ ]
  lib_dirs = [ ]
  if (is_windows) {
    libs += windows_default_libs
    lib_dirs += [ rebase_path(bcs_third_party + "/EWDK_ni_release_svc_prod1_22621_220804-1759/Program Files/Microsoft Visual Studio/2022/BuildTools/VC/Tools/MSVC/14.31.31103/lib/$current_cpu") ]
    lib_dirs += [ rebase_path(bcs_third_party + "/EWDK_ni_release_svc_prod1_22621_220804-1759/Program Files/Windows Kits/10/Lib/10.0.22621.0/um/$current_cpu") ]
    lib_dirs += [ rebase_path(bcs_third_party + "/EWDK_ni_release_svc_prod1_22621_220804-1759/Program Files/Windows Kits/10/Lib/10.0.22621.0/ucrt/$current_cpu") ]
  }
}

config("executable_ldconfig") {
  defines = [ "BCS_EXECUTABLE", "BCS_SHARED_LIBRARY" ]
  ldflags = [ ]
  libs = [ ]
  lib_dirs = [ ]
}

config("static_library_ldconfig") {
  defines = [ "BCS_STATIC_LIBRARY" ]
  ldflags = [ ]
  libs = [ ]
  lib_dirs = [ ]
}

config("shared_library_ldconfig") {
  defines = [ "BCS_SHARED_LIBRARY" ]
  ldflags = [ ]
  libs = [ ]
  lib_dirs = [ ]
  if(is_windows)
  {
    ldflags += [ "/dll" ]
  }
  else if(is_webassembly)
  {
  
  }
  else if(is_linux)
  {
  
  }
}

toolchain("clang") 
{
  alink_ldflags = [ ]
  solink_ldflags = [ ]
  link_ldflags = [ ]
  if (is_windows) 
  {
    alink_ldflags += [ "/out:{{output_dir}}/{{target_output_name}}{{output_extension}}" ]
    solink_ldflags += [ "/out:{{output_dir}}/{{target_output_name}}{{output_extension}}", "/debug", "/pdb:{{output_dir}}/{{target_output_name}}.pdb" ]
    link_ldflags += [ "/out:{{output_dir}}/{{target_output_name}}{{output_extension}}", "/debug", "/pdb:{{output_dir}}/{{target_output_name}}.pdb" ]
  }
  else
  {
    alink_ldflags += [ "-o", "{{output}}" ]
    solink_ldflags += [ "-o", "{{output}}" ]
    link_ldflags += [ "-o", "{{output}}" ]
  }

  tool("cc") {
    depfile = "{{output}}.d"
    command = clang + " -MMD -MF $depfile {{defines}} {{include_dirs}} {{cflags}} {{cflags_c}} -c {{source}} -o {{output}}"
    description = "CC {{output}}"
    outputs = [ "{{target_out_dir}}/{{target_output_name}}$object_extension" ]
    depsformat = "gcc"

    if(is_windows) {
      command += " -fdebug-compilation-dir={{target_out_dir}}/"
    }
  }

  tool("cxx") {
    depfile = "{{output}}.d"
    command = clang + " -MMD -MF $depfile {{defines}} {{include_dirs}} {{cflags}} {{cflags_cc}} -c {{source}} -o {{output}}"
    description = "CXX {{output}}"
    outputs = [ "{{target_out_dir}}/{{target_output_name}}$object_extension" ]
    depsformat = "gcc"

    if(is_windows) {
      command += " -fdebug-compilation-dir={{target_out_dir}}/"
    }
  }

  tool("alink") {
    description = "STATIC LIB LINK {{output}}"
    outputs = [ 
      "{{output_dir}}/{{target_output_name}}{{output_extension}}"
    ]
    default_output_extension = "$static_library_extension"
    default_output_dir = "{{root_out_dir}}/lib/"

    if(is_windows) {
      command = ld + " /lib /out:{{output}} {{inputs}} " + string_join(" ", alink_ldflags)
    } else {
      command = ar + " rcs {{output}} {{inputs}}"
    }
  }

  tool("solink") {
    description = "SHARED LIB LINK {{output}}"
    outputs = [  ]
    if(is_windows) 
    {
      outputs += [ "{{output_dir}}/{{target_output_name}}$static_library_extension" ]
    }
    outputs += [ "{{output_dir}}/{{target_output_name}}{{output_extension}}" ]
    default_output_extension = "$shared_library_extension"
    default_output_dir = "{{root_out_dir}}/bin/"

    command = ld + " {{ldflags}} {{libs}} {{solibs}} {{rlibs}} {{inputs}} " + string_join(" ", solink_ldflags)
  }

  tool("link") {
    description = "LINK {{output}}"
    outputs = [ "{{output_dir}}/{{target_output_name}}{{output_extension}}" ]
    default_output_extension = "$executable_extension"
    default_output_dir = "{{root_out_dir}}/bin/"

    if(is_windows || is_webassembly || is_linux) {
      command = ld + " {{ldflags}} {{libs}} {{solibs}} {{rlibs}} {{inputs}} " + string_join(" ", link_ldflags)
    } else {
      fail("Error: solink unsupported")
    }
  }

  tool("stamp") {
    command = "touch {{output}}"
    description = "STAMP {{output}}"
  }

  tool("copy") {
    command = "cp -af {{source}} {{output}}"
    description = "COPY {{source}} {{output}}"
  }
}
